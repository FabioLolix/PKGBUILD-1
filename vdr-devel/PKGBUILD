# Maintainer: robertfoster

## GLOBALVARS

## VERSIONS
_dvbhddevice="2ea854ae8c7a"
_rahrenbe="2.4.0"

pkgname=vdr-devel
pkgver=2.4.0
pkgrel=1
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
url="ftp://ftp.tvdr.de/vdr/Developer/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('libcap' 'libjpeg-turbo' 'libsystemd' 'perl' 'ttf-font')
makedepends=('systemd') #libsystemd should be enough but the pkg-config file is missing in the libsystemd package
optdepends=('lirc-utils: remote control support'
    'ncurses: skincurses plugin'
'xlogin: To start X11')
replaces=('runvdr-extreme')
conflicts=('runvdr-extreme' 'vdr')
provides=("vdr-api=$pkgver")
install=$pkgname.install
source=("ftp://ftp.tvdr.de/vdr/vdr-$pkgver.tar.bz2"
    "dvbhddevice.zip::https://bitbucket.org/powARman/dvbhddevice/get/${_dvbhddevice}.zip"
    "${pkgname%%-*}.service"
    "${pkgname%%-*}.sysconfig"
    #"${pkgname%%-*}.sysuser.conf"
    "${pkgname%%-*}.sudoers"
    "${pkgname%%-*}-reccmds.conf"
    "${pkgname%%-*}-commands.conf"
    "${pkgname%%-*}-runvdr.sh"
    "${pkgname%%-*}-config.sh"
    "${pkgname%%-*}-skincurses.conf"
    "${pkgname%%-*}-dvbhddevice.conf"
    "${pkgname%%-*}-timercmds.conf"
    "${pkgname%%-*}-shutdown.sh"
    "${pkgname%%-*}-moveto.sh"
    "${pkgname%%-*}-check-setup.sh"
    "${pkgname%%-*}-set-wakeup.sh"
    "${pkgname%%-*}.macros"
    # compilations vars
    Make.config
    # patches
    "http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-${_rahrenbe}-lcn-support-v2.patch.gz"
    "http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-${_rahrenbe}-avcdescriptor.patch.gz"
    "http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-${_rahrenbe}-tpid.patch.gz"
    "http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-${_rahrenbe}-networkname.patch.gz"
    "http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-${_rahrenbe}-editrecording.patch.gz"
    "http://www.saunalahti.fi/~rahrenbe/vdr/patches/vdr-${_rahrenbe}-resumereset.patch.gz"
)
# backup=('etc/vdr/conf.avail/"50-dvb{s,h}ddevice.conf'
#        'etc/vdr/conf.avail/"50-{epgtableid0,hello,osddemo,pictures,rcu,skincurses,status,svdrpdemo}.conf'
#        'etc/vdr/conf.avail/"50-svc{cli,svr}.conf'
#        'etc/vdr/conf.d/00-vdr.conf'
#        'var/lib/vdr/channels.conf'
#        'var/lib/vdr/diseqc.conf'
#        'var/lib/vdr/keymacros.conf'
#        'var/lib/vdr/scr.conf'
#        'var/lib/vdr/sources.conf'
#        'var/lib/vdr/svdrphosts.conf'
# )

prepare() {
    cd "${srcdir}/vdr-${pkgver}"

    cp -p ../"${pkgname%%-*}-reccmds.conf" reccmds.conf
    cp -p ../"${pkgname%%-*}-timercmds.conf" timercmds.conf
    cp -p ../"${pkgname%%-*}-commands.conf" commands.conf
    # Unfortunately these can't have comments in them, so ship 'em empty.
    cat /dev/null > channels.conf
    cat /dev/null > remote.conf
    cat /dev/null > setup.conf
    cat /dev/null > timers.conf

    msg2 "dvbhddevice plugin"
    cp -r "$srcdir/powARman-dvbhddevice-${_dvbhddevice}" PLUGINS/src/dvbhddevice

    msg2 "Rolf Ahrenberg patches"
    patch -Np1 -i ../"vdr-${_rahrenbe}-lcn-support-v2.patch"
    patch -Np1 -i ../"vdr-${_rahrenbe}-avcdescriptor.patch"
    patch -Np1 -i ../"vdr-${_rahrenbe}-tpid.patch"
    patch -Np1 -i ../"vdr-${_rahrenbe}-networkname.patch"
    patch -Np1 -i ../"vdr-${_rahrenbe}-editrecording.patch"
    patch -Np1 -i ../"vdr-${_rahrenbe}-resumereset.patch"

    msg2 "Generating system config"
    cat $srcdir/Make.config > Make.config
}

build() {
    cd "${srcdir}/vdr-${pkgver}"
    make
}

package() {
    cd "${srcdir}/vdr-${pkgver}"
    make DESTDIR="$pkgdir" install

    cd ..
    install -pm 755 "${pkgname%%-*}-runvdr.sh" 	"$pkgdir/usr/bin/runvdr"
    install -Dm 644 "${pkgname%%-*}.service" 	"$pkgdir/usr/lib/systemd/system/${pkgname%%-*}.service"
    install -dm 750 "$pkgdir/etc/sudoers.d"
    install -Dpm 440 "${pkgname%%-*}.sudoers" "$pkgdir/etc/sudoers.d/vdr"
    install -Dm 644 "${pkgname%%-*}.sysconfig" "$pkgdir/etc/conf.d/vdr"
    install -m 755 "${pkgname%%-*}-shutdown.sh" "$pkgdir/usr/bin/${pkgname%%-*}-shutdown.sh"
    install -m 755 "${pkgname%%-*}-moveto.sh" "$pkgdir/usr/bin/${pkgname%%-*}-moveto.sh"
    install -m 755 "${pkgname%%-*}-check-setup.sh" "$pkgdir/usr/bin/${pkgname%%-*}-check-setup"
    install -m 755 "${pkgname%%-*}-set-wakeup.sh" "$pkgdir/usr/bin/${pkgname%%-*}-set-wakeup"
    
    sed -i -e "s|VDR_PLUGIN_VERSION|$pkgver|" "$pkgdir/usr/bin/runvdr"
    #install -Dm755 "${pkgname%%-*}.sysuser.conf" "$pkgdir/usr/lib/sysusers.d/${pkgname%%-*}.conf"
}

