# Maintainer: robertfoster

pkgname=liquidsoap
pkgver=1.4.4
pkgrel=1
pkgdesc="A swiss-army knife for multimedia streaming, notably used for netradios and webtvs"
arch=('i686' 'x86_64')
url="https://www.liquidsoap.info"
license=('GPL')
depends=('fluidsynth' 'giflib' 'gst-plugins-good' 'gst-plugins-ugly'
  'ocaml-alsa'
  'ocaml-ao'
  'ocaml-bjack'
  'ocaml-camomile'
  'ocaml-cry'
  'ocaml-dssi'
  'ocaml-dtools'
  'ocaml-duppy'
  'ocaml-faad'
  'ocaml-fdkaac'
  'ocaml-ffmpeg'
  'ocaml-flac'
  'ocaml-frei0r'
  'ocaml-gavl'
  'ocaml-gd4o'
  'ocaml-gstreamer'
  'ocaml-inotify'
  'ocaml-yojson'
  'ocaml-ladspa'
  'ocaml-lame'
  'ocaml-lastfm'
  'ocaml-lo'
  'ocaml-mad'
  'ocaml-magic'
  'ocaml-menhir'
  'ocaml-mm'
  'ocaml-ocamlsdl'
  'ocaml-ogg'
  'ocaml-opus'
  'ocaml-pulseaudio'
  'ocaml-samplerate'
  'ocaml-sedlex'
  'ocaml-shine'
  'ocaml-soundtouch'
  'ocaml-speex'
  'ocaml-taglib'
  'ocaml-theora'
  'ocaml-vorbis'
  'ocaml-xmlplaylist'
)
makedepends=('camlp4' 'libxml-perl' 'ocaml-findlib' 'ocaml-menhir' 'ocaml-pcre' 'ocaml-sedlex' 'perl-xml-dom')
optdepends=('curl')
options=('!makeflags')
source=("https://github.com/savonet/liquidsoap/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"
  $pkgname.service
  $pkgname.tmpfilesd
  5d7f810694caaf181e8fe97c14b836ca86e47cd5.patch
)

install=$pkgname.install
conflicts=('liquidsoap-git' 'liquidsoap-full')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../5d7f810694caaf181e8fe97c14b836ca86e47cd5.patch
  #  sed -i "s|bashcompdir=|bashcompdir=${pkgdir//\//\\/}|g" configure.ac
  #  sed -i "s|emacsdir=|emacsdir=${pkgdir//\//\\/}|g" configure.ac
  ./bootstrap
  ./configure --prefix=/usr --localstatedir=/var --sysconfdir=/etc \
  --without-user --without-group \
  --disable-oss
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make all
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  make DESTDIR="$pkgdir" datadir="$pkgdir/usr/share/" mandir="$pkgdir/usr/share/man/" localstatedir="$pkgdir/var" bindir="$pkgdir/usr/bin/" libdir="$pkgdir/usr/lib/" sysconfdir="$pkgdir/etc/" install
  
  # Install systemd unit
  install -Dm0644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/liquidsoap@.service"
  # Install the tmpfilesd file
  install -Dm0644 $srcdir/$pkgname.tmpfilesd $pkgdir/usr/lib/tmpfiles.d/liquidsoap.conf
}

sha256sums=('e3aa0983ae6de51261391343963e9b9aa85444c8e4e91fde716abc92462a1712'
            'df6d2cec1be47a57a02ed04a1f527c0349221fad39d8d152aca13734d3808661'
            '9f286958af0c751c2a43d74614cdd1c4629c0583d619875385c09417a5383675'
            '9009b01abf9b41c02b9ec03e5d14e24e3030133e60d2074a3a942a23247b7d42')
